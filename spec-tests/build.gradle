plugins {
    id "java"
    id "checkstyle"
    id "test-logging-convention"
}
apply from: "../dependencies.gradle"

repositories {
    mavenCentral()
}

description = "Candlepin Spec Tests"

dependencies {
    implementation project(":client")

    implementation libraries["gson"]
    implementation libraries["okhttp"]
    implementation libraries["okhttpTls"]
    implementation libraries["slf4j"]
    implementation libraries["junitApi"]
    implementation libraries["junitParams"]
    implementation libraries["jacksonDatabind"]
    implementation libraries["jacksonJdk8"]
    implementation libraries["jacksonJsr310"]
    implementation libraries["assertj"]
    implementation libraries["commonsCodec"]
    implementation libraries["guava"]
    implementation libraries["commonsIo"]
    implementation libraries["oauth"]

    testImplementation libraries["jimfs"]
    testImplementation libraries["awaitility"]

    testRuntimeOnly libraries["junitEngine"]

    checkstyle libraries["checkstyle"]
    checkstyle libraries["checkstyleSevntu"]

    implementation fileTree(dir: '/usr/lib64/jss', include: '*.jar')
}

testlogger {
    theme = "standard-parallel"
}

// Disable empty test task
test {
    enabled = false
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task spec(type: Test) {
    description = 'Run Java based spec tests'
    group = 'Verification'
    outputs.upToDateWhen { false }

    debugOptions {
        host = '*'
    }

    useJUnitPlatform()

    // We have to propagate the -D params if we want them available in tests
    System.properties.keys().each { key ->
        def properyKey = key.toString()
        // Propagate spec config
        if (properyKey.startsWith("spec.test.client")) {
            systemProperty properyKey, System.getProperty(properyKey)
        }

        // Propagate current working directory
        if (properyKey == "user.dir") {
            systemProperty properyKey, System.getProperty(properyKey)
        }
    }
}

// Create a single checkstyle task to make it easier to run both the checkstyleMain & checkstyleTest targets
task checkstyleSpec(dependsOn: [tasks.checkstyleMain, tasks.checkstyleTest]) {
    description = "Run checkstyle for both the Main & Test targets"
    group = "Verification"
}
