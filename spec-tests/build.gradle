plugins {
    id "java"
    id "checkstyle-conventions"
    id "test-logging-conventions"
}
apply from: "../dependencies.gradle"

repositories {
    mavenCentral()
}

description = "Candlepin Spec Tests"

dependencies {
    implementation project(":client")

    implementation libraries["gson"]
    implementation libraries["okhttp"]
    implementation libraries["okhttpTls"]
    implementation libraries["slf4j"]
    implementation libraries["junitApi"]
    implementation libraries["junitParams"]
    implementation libraries["jacksonDatabind"]
    implementation libraries["jacksonJdk8"]
    implementation libraries["jacksonJsr310"]
    implementation libraries["assertj"]
    implementation libraries["commonsCodec"]
    implementation libraries["guava"]
    implementation libraries["commonsIo"]
    implementation libraries["oauth"]

    testImplementation libraries["jimfs"]
    testImplementation libraries["awaitility"]

    testRuntimeOnly libraries["junitEngine"]

    implementation fileTree(dir: '/usr/lib64/jss', include: '*.jar')
}

testlogger {
    theme = "standard-parallel"
}

// Disable empty test task
test {
    enabled = false
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.register('spec', Test) {
    description = 'Run Java based spec tests'
    group = 'Verification'
    outputs.upToDateWhen { false }

    debugOptions {
        host = '*'
    }

    useJUnitPlatform()
    // TODO Find better value for concurrency limit that doesn't break jenkins
    // maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    reports.html.required = false
    reports.junitXml.required = false

    // We have to propagate the -D params if we want them available in tests
    System.properties.keys().each { key ->
        def properyKey = key.toString()
        // Propagate spec config
        if (properyKey.startsWith("spec.test.client")) {
            systemProperty properyKey, System.getProperty(properyKey)
        }

        // Propagate current working directory
        if (properyKey == "user.dir") {
            systemProperty properyKey, System.getProperty(properyKey)
        }
    }
}
