plugins {
    id "java"
    id "groovy-gradle-plugin"
    id "java-gradle-plugin"
}

apply from: "../dependencies.gradle"

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation(gradleApi())
    implementation(libraries["openapi"])

    // add all plugin artifacts to gradle classpath as defined in dependencies.gradle
    // this makes them available via plugin ID only across *all* projects
    for (pluginDependency in project.ext.plugins) {
        implementation pluginDependency
    }

    // Fix for transitive dependency conflict between openapi and owasp
    configurations.configureEach {
        resolutionStrategy {
            force 'org.yaml:snakeyaml:2.0'
        }
    }
}

gradlePlugin {
    plugins {
        specVersionPlugin {
            id = "org.candlepin.gradle.SpecVersion"
            implementationClass = "org.candlepin.gradle.SpecVersion"
        }
        gettextPlugin {
            id = "org.candlepin.gradle.gettext"
            implementationClass = "Gettext"
        }
        msgfmtPlugin {
            id = "org.candlepin.gradle.msgfmt"
            implementationClass = "Msgfmt"
        }

        pomToolkitPlugin {
            id = "org.candlepin.gradle.PomToolkit"
            implementationClass = "PomToolkit"
        }
    }
}
