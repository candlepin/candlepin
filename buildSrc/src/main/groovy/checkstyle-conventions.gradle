plugins {
    id("checkstyle")
}
project.apply from: "${rootDir}/dependencies.gradle"

configurations {
    checkstyle
}

dependencies {
    checkstyle libraries["checkstyle"]
    checkstyle libraries["checkstyleSevntu"]
}

// This prevents error where checkstyle sometimes resolves its dependencies
// to use google-collections instead of guava.
configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:0")
    }
}

checkstyle {
    // Use the latest checkstyle version instead of default one
    def module = project.dependencies.create(libraries["checkstyle"])
    def version = module.version
    toolVersion = version
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    maxWarnings = 0
    maxErrors = 0
}

checkstyleMain.exclude("**/i18n/Messages*")

// Create a single checkstyle task to make it easier to run the checkstyleMain
// and checkstyleTest targets at once.
tasks.register("checkstyle") {
    dependsOn tasks.checkstyleMain
    dependsOn tasks.checkstyleTest
    description = 'Run checkstyle for the Main and Test targets at once'
    group = 'Verification'
}