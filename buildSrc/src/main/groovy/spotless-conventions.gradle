import com.diffplug.spotless.LineEnding

plugins {
    id("com.diffplug.spotless")
}

spotless {

    format("misc") {
        target("*.gradle", "buildSrc/**/*.gradle", "*.gitignore")
        targetExclude("**/build/**")
        ratchetFrom 'origin/main'

        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }

    def importOrderConfigFile = rootProject.file("config/codestyle/eclipse.importorder")
    def javaFormatterConfigFile = rootProject.file("config/codestyle/eclipse-formatter.xml")
    def licenseHeader = rootProject.file("config/codestyle/licence-header.txt")

    java {
        targetExclude("**/build/**")
        ratchetFrom 'origin/main'

        // License
        licenseHeaderFile(licenseHeader, "(package|import|open|module) ")

        // Imports
        importOrderFile(importOrderConfigFile)
        removeUnusedImports()

        // Formatting
        eclipse().configFile(javaFormatterConfigFile)
        trimTrailingWhitespace()
        endWithNewline()
    }

    lineEndings = LineEnding.UNIX

}
